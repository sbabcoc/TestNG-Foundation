import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'jacoco'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
  id 'pl.allegro.tech.build.axion-release' version '1.13.14'
}

group = 'com.nordstrom.tools'
description = 'TestNG Foundation'

if (!project.hasProperty('profile')) {
  ext.profile = 'java8'
} else {
  assert ['java8'].contains(profile)
}

apply from: "${profile}Deps.gradle"

def archiveVer = null
def verBits = scmVersion.version.split('-')
def javaVersion = 'j' + profile.charAt(4)
if (verBits.length > 1) {
  if (verBits[1].equals(javaVersion)) {
    archiveVer = project.version
  } else {
    archiveVer = verBits[0] + '-' + javaVersion + '-' + verBits[1]
  }
} else {
  archiveVer = verBits[0] + '-' + javaVersion
}

project.version = archiveVer
  
def archiveBase = rootProject.name + '-' + archiveVer

java {
  withJavadocJar()
  withSourcesJar()
}

clean {
  delete 'logs'
  delete 'target'
}

jacoco {
  toolVersion = '0.8.8'
  reportsDirectory = file("${buildDir}/customJacocoReportDir")
}

jacocoTestReport {
  reports {
    xml.enabled false
    csv.enabled false
    html.destination file("${buildDir}/jacocoHtml")
  }
}

jar {
  group 'Build'
  description "Assembles a jar archive containing the '${profile}' classes, POM and Maven properties."
  
  def destPath = "META-INF/maven/${project.group}/${rootProject.name}"
  def timestamp = Long.valueOf(System.currentTimeMillis()).toString()
  def pomTokens = [projectVersion: archiveVer, projectTimestamp: timestamp, javaVersion: javaVersion]
  def propTokens = [projectVersion: archiveVer, projectGroupId: project.group, projectArtifactId: rootProject.name]
  
  from('.') {
    include('pom.xml')
    into(destPath)
    filter(ReplaceTokens, tokens: pomTokens)
  }
  from('.') {
    include('pom.properties')
    into(destPath)
    filter(ReplaceTokens, tokens: propTokens)
  }
  archiveFileName = archiveBase + '.jar'
  destinationDirectory = libsDir
}

clean {
  delete buildRoot
}

sourcesJar {
  group 'Build'
  description "Assembles a jar archive containing the '${profile}' source files."
  
  classifier = 'sources'
  from sourceSets.main.allSource
  archiveFileName = archiveBase + '-sources.jar'
  destinationDirectory = libsDir
}

javadocJar {
  dependsOn javadoc
  group 'Build'
  description "Assembles a jar archive containing the '${profile}' JavaDoc files."
  
  classifier = 'javadoc'
  from javadoc.destinationDir
  archiveFileName = archiveBase + '-javadoc.jar'
  destinationDirectory = libsDir
}

test {
  useTestNG()
  include '**/*Test.class'
  reports.html.destination = file("${buildDir}/reports/testng")
  testLogging.showStandardStreams = true
}

artifacts {
  archives tasks['sourcesJar']
  archives tasks['javadocJar']
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)
      pom {
        name = 'TestNG Foundation'
        groupId = project.group
        artifactId = rootProject.name
        version = archiveVer
        packaging = 'jar'
        description = 'TestNG Foundation is an automation framework designed to extend and enhance the capabilities provided by TestNG.'
        url = 'https://github.com/sbabcoc/TestNG-Foundation'

        scm {
          connection = 'scm:git:https://github.com/sbabcoc/TestNG-Foundation.git'
          developerConnection = 'scm:git:https://github.com/sbabcoc/TestNG-Foundation.git'
          url = 'https://github.com/sbabcoc/TestNG-Foundation/tree/master'
          tag = 'HEAD'
        }

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'scoba'
            name = 'Scott Babcock'
            email = 'scoba@hotmail.com'
            organization = 'Nordstrom'
            organizationUrl = 'https://shop.nordstrom.com'
          }
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

task install {
  dependsOn publishToMavenLocal
  group publishToMavenLocal.group
  description '[alias] ' + publishToMavenLocal.description
}

nexusPublishing {
  packageGroup = 'com.nordstrom'
  repositories {
    ossrh {
      nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
      snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
      if (project.hasProperty('ossrhStagingProfileId')) {
        stagingProfileId = ossrhStagingProfileId
      }
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://repo1.maven.org/maven2' }
  maven { url 'https://repo.maven.apache.org/maven2' }
  maven { url "${projectDir}/repo" }
}

dependencies {
  constraints {
    api 'com.nordstrom.tools:java-utils:3.0.0'
    api 'com.nordstrom.tools:settings:3.0.1'
  }
  api 'com.nordstrom.tools:java-utils'
  api 'com.nordstrom.tools:settings'
}
